apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def siteUrl = 'https://github.com/wuyr/SecondFloorBehavior' //需要修改
def gitUrl = 'https://github.com/Ifxcyr/SecondFloorBehavior.git' //需要修改

version = "1.0.4"
group = "com.wuyr"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.1"


    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    pkg {
        repo = 'SecondFloorBehavior' //需要修改
        name = 'SecondFloorBehavior' //需要修改
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        userOrg = 'wuyr'
        publish = true

        version {
            name = '1.0.4'
            desc = '通过自定义Behavior实现“二楼”效果，兼容所有下拉刷新框架' //需要修改
            released = new Date()
            vcsTag = '1.0.4'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
    configurations = ['archives']
}

install {
    repositories.mavenInstaller {

        pom {
            project {
                packaging 'aar'

                name '陈小缘'
                description '通过自定义Behavior实现“二楼”效果，兼容所有下拉刷新框架' //需要修改
                url siteUrl

                licenses {
                    license {
                        name 'Apache-2.0'
                        url 'https://raw.githubusercontent.com/Ifxcyr/SecondFloorBehavior/master/LICENSE' //需要修改
                    }
                }
                developers {
                    developer {
                        id 'ifxcyr'
                        name '陈小缘'
                        email 'ifxcyr@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

dependencies {
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.0.0'
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/8/docs/api"
    }
}